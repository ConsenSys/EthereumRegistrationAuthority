apply plugin: 'java'
apply plugin: 'application'

version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

mainClassName = 'tech.pegasys.era.tools.EraNetworkConfig'

// build extra start script for EraViewer
task buildEraViewerScript(type: CreateStartScripts) {
    description "Creates OS specific scripts to call the 'other' entry point"
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
    mainClassName = 'tech.pegasys.era.tools.EraViewer'
    applicationName = 'eraviewer'
}

// build extra start script for EraDeploy
task buildEraDeployScript(type: CreateStartScripts) {
    description "Creates OS specific scripts to call the 'other' entry point"
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
    mainClassName = 'tech.pegasys.era.tools.EraDeploy'
    applicationName = 'eradeploy'
}

// build start scripts
// Note: This causes double overwrite during unzip of poc.zip and -o option is
//       therefore required. This approach is temporary and good enough for now.
distZip {
    baseName = archivesBaseName
    //include our extra start scripts
    into("${baseName}/bin") {
        from buildEraViewerScript
        fileMode = 0755
        from buildEraDeployScript
        fileMode = 0755
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.web3j:core:4.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.2.0'
    compile group: 'org.slf4j', name:'slf4j-api', version: '1.7.2'
    compile group: 'ch.qos.logback', name:'logback-classic', version: '1.0.9'
    compile group: 'ch.qos.logback', name:'logback-core', version: '1.0.9'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}